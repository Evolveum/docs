== Axiom Overview

=== Functional Overview

Axiom is representation-independent data modeling language.
Axiom can be used to model data in their abstract form.
Unlike other modeling languages, Axiom is not bound to specific data representation from such as JSON or XML.
Data modeled in Axiom can be represented (serialized and parsed) in many representation formats.
Simply speaking, the same data can be expressed in XML, JSON, YAML and possibly also in other formats, including Axiom itself.

A Axiom model defines data objects and their nested data item hierarchies.
This allows a complete human and machine-readable description of the data.

Axiom models the entities as uniquely identifiable _objects_.
Object is hierarchical organization of its data as a tree of _items_ in which each item has a name, and either a value (_property_) or a set of child items (_container_).
Axiom provides clear and concise descriptions of the items, as well as relations between those items.

Axiom structures data into models.
A model can import definitions from other external models.
The hierarchy can be extended, allowing one model to add items to the extension point defined in another model.

Axiom data models can describe constraints to be enforced on the data.

Axiom defines a set of built-in types and has a type mechanism through which additional types may be defined.
Derived types can restrict their base type's set of valid values using mechanisms like range or pattern restrictions and extend their base type by addition of additional items in case of complex types.

Axiom is a self-descriptive language.
Axiom modeling language could be also modeled using Axiom.

=== Language Overview

==== Models

Axiom data structure is defined in models.
A model contains collection of related definitions such as object and type definitions.

A model contains three types of statements: model header statements, versioning statements and definition statements.

==== Data Modeling Basics

Axiom defines five basic concepts for data modeling - objects, properties, containers, object references and value metadata.

Objects are top-level data structures.
Objects are a set of _items_: properties, containers and references.
_Properties_ contain coherent data structures that are usually simple primitive values.
_Containers_ can be used to group other items in hierarchical data structures.
_References_ can be used to express relations between objects.

===== Objects

Object represents top-level entity, which may have nested data items.
Object must be uniquely identifiable in the system.
E.g. in Prism this is achieved by objects having *object identifier* (UUID).
Objects contain data _items_.

.Axiom Example
[source, axiom]
----

object User {
  itemName user;
  description """
    User of the system.
    """;


  property oid {
    type uuid;
  }
  property name {
    type PolyString;
  }
}
----

.XML Encoding
[source,xml]
----
<user>
  <oid>0b2f5d5c-6560-43da-9dcb-445eb6fd742a</oid>
  <name>administrator</name>
  ...
</user>
----

.YAML Encoding
[source, yaml]
----
user:
  oid: 0b2f5d5c-6560-43da-9dcb-445eb6fd742a
  name: administrator
  ...
----

===== Properties

A property instance contains data of simple type like an integer or a string.
It may have multiple values of particular type.
In case of *expanded value form* it may have nested data items.

[source, axiom]
----
property comment {
  type PolyString;
}
----

[source, xml]
----
<comment>Simple value</comment>
<comment>
    <orig>Complex Value</orig>
    <norm>complex-value</norm>
    <metadata>
      ...
    </metadata>
</comment>
----


[source, yaml]
----
comment:
  - Simple value
  - orig: Complex value
    norm: complex-value
    metadata:
      ....
----

===== Containers

A container instance contains data of complex type like address or user details.
It may have multiple values of particular type.

[source,axiom]
----
complexType Address {
  property street {
    type string;
  }
  property city {
    type string;
  }
  property country {
    type string;
  }
}

container primaryAddress {
  type Address;
}
----

[source,yaml]
----
primaryAddress:
  street: Main Street 10
  city: Bratislava
  country: Slovak Republic
----

===== Object references
An object reference instance contains reference to object of particular type.
It may have multiple references to particular object.

.Axiom Example
[source, axiom]
----
resourceReference managerRef {
  description "Manager of the resource";
  type User;
}
----

[source, yaml]
----
managerRef:
  oid: 0b2f5d5c-6560-43da-9dcb-445eb6fd742a
----


==== Built-in Types

Axiom provides following built-in simple types:

.Built-in types from XSD
boolean,
base64,
byte,
date,
dateTime,
dateTimeStamp,
decimal,
double,
duration,
float,
int,
integer,
language,
long,
negativeInteger,
nonNegativeInteger,
nonPositiveInteger,
normalizedString,
positiveInteger,
precisionDecimal,
QName,
short,
string,
time,
token,
unsignedByte,
unsignedInt,
unsignedLong,
unignedShort,
yearMonthDuration,

.Axiom Specific Built in types
AxiomIdentifier:: Represent item or type identifier
PolyString:: String which can have normalized form and language-specific variants defined.

==== Derived Types

Axiom allows type derivation for both simple andÂ complex types.
Derived simple type may limit possible value set by imposing restrictions.
Derived complex type may introduce additional items (properties, container, object references).

.Axiom Example
----
type IdentifiableObject {
  property oid {
    type uuid;
  }
  identifier oid;
}

type User {
  extends IdentifiableObject;
  property username {
    type PolyString;
  }
}
----

==== Extending Data Models

Axiom allows for model to introduce extension to other existing model,
which has defined extension points.

[source, axiom]
----
model baseline {
  namespace "urn:ns:baseline";

  type User {
    property username;
    container extensions;
  }

  object user {
    type User;
  }
}

model custom-ext {
  namespace "urn:ns:extension";
  import baseline;

  extension UserExtension {
    target User;
    property departmentId {
      type string;
    }
  }
}
----

.XML example
[source,xml]
----
<user>
  <username>administrator</username>
  <extensions>
    <departmentId xmlns="urn:ns:extension">DEP-01</departmentId>
  </extensions>
</user>

----

.YAML example
[source, yaml]
----
user:
  username: administrator
  extensions:
    "urn:ns:extension":
       departmentId: DEP-01

----

== Axiom Background

Axiom is a language originally designed to model data in midPoint project.
MidPoint was originally designed with XML and XML Schema Defintion (XSD), however those mechanisms were never a perfect fit for project needs.
Finally in 2020 it was decided to replace XSD with a new data modeling language that would be a better fit.
First step was to use Axiom to represent meta-data schema (meta-schema) for data provenance purposes needed by MidPrivacy initiative.

Axiom was used to model _Prism_ data structures in midPoint.
Prism is a Java framework that can be used to maintain object-based data models.
Prism specifies basic properties of a data object, it defines data structures that describe object changes (_deltas_), it specifies query language for objects and so on.
Prism evolved in midPoint project where it was used as a basic data framework.
